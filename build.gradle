import org.gradle.api.artifacts.*

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/plugins-release' }
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.spring.io/libs-snapshot' }
    }
    
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.5.RELEASE'
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

apply plugin: 'base' // To add "clean" task to the root project.

ext {
    springRestdocsVersion = '1.1.2.RELEASE'
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'propdeps-maven'
    apply from: rootProject.file('common.gradle')
    
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
    
    dependencyManagement {
        dependencies {
            dependency 'com.fasterxml.jackson.core:jackson-databind:2.4.6.1'
            dependency "org.springframework.restdocs:spring-restdocs-core:$springRestdocsVersion"
            dependency 'org.jsoup:jsoup:1.9.1'
            dependency 'commons-io:commons-io:2.5'

            dependency 'junit:junit:4.12'
            dependency 'org.hamcrest:hamcrest-core:1.3'
            dependency 'org.hamcrest:hamcrest-library:1.3'
            dependency 'org.mockito:mockito-core:1.10.19' 
        }
    }
    
    test {
        testLogging {
            exceptionFormat "full"
            showStandardStreams = true
        }
    }
}

task mergedJavadoc(type: Javadoc, description: 'Creates Javadoc from all the projects.') {
    title = 'All modules'
    destinationDir = new File(project.buildDir, 'merged-javadoc')

    // Note: The closures below are executed lazily.
    source {
        subprojects*.sourceSets*.main*.allSource
    }
    classpath.from {
        subprojects*.configurations*.compile*.copyRecursive({ !(it instanceof ProjectDependency); })*.resolve()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}


